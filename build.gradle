buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java-library'
    id 'application'
    id 'io.qameta.allure' version '2.8.1'
}

group 'com.example'
version '1.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8
defaultTasks 'help'

ext {
    allureVersion = "2.13.8"
    selenideVersion = "5.24.0"
    junitVersion = "5.8.1"
}

repositories {
    maven {url = ""}
    mavenCentral()
}

allure {
    version = "${allureVersion}"
    autoconfigure = true
    aspectjweaver = true
    useJUnit5 {
        version = "${allureVersion}"
    }
}

dependencies {
    testImplementation (
            "org.aeonbits.owner:owner:1.0.12",
            "org.aspectj:aspectjweaver:1.9.5",
            "com.codeborne:selenide:${selenideVersion}",
            "io.qameta.allure:allure-selenide:${allureVersion}",
            "io.rest-assured:rest-assured:4.2.0",
            "io.qameta.allure:allure-rest-assured:${allureVersion}",
            "com.fasterxml.jackson.core:jackson-annotations:2.10.3",
            "io.appium:java-client:7.3.0",
            "ch.qos.logback:logback-classic:1.2.3",
//            "io.qameta.allure:allure-ee-junit-platform:3.28.2",
            "org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'

    testCompileOnly 'org.projectlombok:lombok:1.18.16'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.16'
}

configurations {
    testCompile {
        extendsFrom testImplementation
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperties = System.properties
    if (project.hasProperty("web_threads")) {
        systemProperties += [
                'junit.jupiter.execution.parallel.enabled'                 : true,
                'junit.jupiter.execution.parallel.mode.default'            : 'concurrent',
                'junit.jupiter.execution.parallel.mode.classes.default'    : 'concurrent',
                'junit.jupiter.execution.parallel.config.strategy'         : 'fixed',
                'junit.jupiter.execution.parallel.config.fixed.parallelism': project.getProperty("web_threads").toInteger()
        ]
    }
    if (project.hasProperty("allure_results")) {
        systemProperty 'allure_results', project.getProperty("allure_results")
    }
    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
    systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
}

task tests(type: Test) {
    useJUnitPlatform {}
}

task test_env(type: Test) {
    useJUnitPlatform {
        includeTags 'TEST'
    }
}

task dev_env(type: Test) {
    useJUnitPlatform {
        includeTags 'DEV'
    }
}

task prod_env(type: Test) {
    useJUnitPlatform {
        includeTags 'PROD'
    }
}

task web(type: Test) {
    useJUnitPlatform {
        includeTags 'web'
    }
    systemProperty 'platform', 'web'
}

task android(type: Test) {
    useJUnitPlatform {
        includeTags 'android'
    }
    systemProperty 'platform', 'android'
}

task ios(type: Test) {
    useJUnitPlatform {
        includeTags 'ios'
    }
    systemProperty 'platform', 'ios'
}

task ui(type: Test) {
    useJUnitPlatform {
        includeTags 'ui'
    }
    systemProperty 'platform', 'web'
}

help {
    doFirst {
        println ""
        println "************************************************************************ "
        println "***                                                                  *** "
        println "*** Welcome to                                                       *** "
        println "*** https://github.com/autotests-cloud/selenide-junit5-allure-gradle *** "
        println "***                                                                  *** "
        println "*** Requirements for mobile tests:                                   *** "
        println "*** - start appium server locally                                    *** "
        println "*** - install XCode with iPhone Simulators                           *** "
        println "***                                                                  *** "
        println "*** You can run tests by one of the following commands:              *** "
        println "*** ./gradlew web                                                    *** "
        println "*** ./gradlew android                                                *** "
        println "*** ./gradlew ios                                                    *** "
        println "***                                                                  *** "
        println "************************************************************************ "
        println ""
    }
}
